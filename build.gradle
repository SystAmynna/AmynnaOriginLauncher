plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'

    id 'org.openjfx.javafxplugin' version '0.0.13' // Ajout du plugin JavaFX
}

group = 'com.amynna'
version = '0.1.1'

repositories {
    mavenCentral()
    maven {url 'https://litarvan.github.io/maven'}
    //maven {url 'https://KinjerJS.github.io/maven'}
    maven {url "https://maven.minecraftforge.net/" }
}

javafx {
    version = "21"
    modules = [ 'javafx.swing', 'javafx.web' ]
}

ext {
    bootstrapName = 'OriginRP'
    LauncherName = 'OriginRP_Launcher'

    bouncyCastleVersion = '1.78'
    globalDeps = [
            'org.bouncycastle:bcprov-jdk18on:' + bouncyCastleVersion,
            'org.bouncycastle:bcpkix-jdk18on:' + bouncyCastleVersion,
            'org.json:json:20231013',
    ]

    launcherDeps = [
            'fr.litarvan:openauth:1.1.6',
            //'com.github.KinjerJS:OpenAuth:1.2.0',
            'net.java.dev.jna:jna:5.13.0',
            'net.java.dev.jna:jna-platform:5.13.0',
            'net.minecraftforge:forge:1.20.1-47.4.0:installer',
    ]
}

dependencies {

    // TESTS
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // GLOBAL
    globalDeps.each { implementation it }

    // LAUNCHER
    launcherDeps.each { implementation it }

}

test {
    useJUnitPlatform()
}

tasks.withType(JavaCompile).configureEach {
    options.release = 17
}

task OriginJar(type: Jar) {
    archiveBaseName.set(bootstrapName)
    archiveClassifier.set('')
    archiveVersion.set(version)

    // Inclure les packages OriginBootstrap et Tools
    from(sourceSets.main.output) {
        include 'com/amynna/OriginBootstrap/**'
        include 'com/amynna/Tools/**'
        exclude 'com/amynna/OriginLauncher/**'
    }

    // Inclure uniquement les dépendances Bouncy Castle
    from {
        configurations.runtimeClasspath.findAll { dep ->
            globalDeps.any { bcDep ->
                dep.name.contains(bcDep.split(':')[1].split('-')[0])
            }
        }.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }

    // Exclure les fichiers de signature qui causent des conflits
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
    exclude 'META-INF/MANIFEST.MF'

    // Éviter les doublons de fichiers META-INF
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    manifest {
        attributes 'Main-Class': 'com.amynna.OriginBootstrap.App'
    }
}

task LauncherJar(type: Jar) {
    archiveBaseName.set(LauncherName)
    archiveClassifier.set('')
    archiveVersion.set(version)

    // Inclure les packages OriginLauncher et Tools
    from(sourceSets.main.output) {
        include 'com/amynna/OriginLauncher/**'
        include 'com/amynna/Tools/**'
        exclude 'com/amynna/OriginBootstrap/**'
    }

    // Inclure les dépendances globalDeps et launcherDeps
    from {
        configurations.runtimeClasspath.findAll { dep ->
            globalDeps.any { bcDep ->
                dep.name.contains(bcDep.split(':')[1].split('-')[0])
            } || launcherDeps.any { lDep ->
                dep.name.contains(lDep.split(':')[1].split('-')[0])
            }
        }.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }

    // Exclure les fichiers de signature qui causent des conflits
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
    exclude 'META-INF/MANIFEST.MF'

    // Éviter les doublons de fichiers META-INF
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    manifest {
        attributes 'Main-Class': 'com.amynna.OriginLauncher.App'
    }
}
