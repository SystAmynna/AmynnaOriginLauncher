plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'fr.amynna'
version = '1.0-SNAPSHOT'

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

repositories {
    mavenCentral()
    maven {url 'https://litarvan.github.io/maven'}
    maven { url "https://maven.minecraftforge.net/" }
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    implementation 'org.json:json:20231013'
    implementation 'fr.litarvan:openauth:1.1.3'

    implementation 'net.java.dev.jna:jna:5.13.0'
    implementation 'net.java.dev.jna:jna-platform:5.13.0'

    implementation "net.minecraftforge:forge:1.20.1-47.4.0:installer"
}

test {
    useJUnitPlatform()
}

tasks.withType(JavaCompile) {
    options.release = 17
}

shadowJar {
    archiveBaseName.set('archive')
    archiveClassifier.set('')
    archiveVersion.set(version)
    manifest {
        attributes 'Main-Class': 'fr.amynna.OriginLauncher.App'
    }
}

// Tâche pour créer un JAR contenant uniquement le package KeyUtil
task keyUtilJar(type: Jar) {
    archiveBaseName.set('keyutil')
    archiveClassifier.set('')
    archiveVersion.set(version)

    // Inclure uniquement les fichiers du package fr.amynna.KeyUtil
    from(sourceSets.main.output) {
        include 'fr/amynna/KeyUtil/**'
    }

    manifest {
        attributes 'Implementation-Title': 'KeyUtil Library',
                'Implementation-Version': version
        attributes 'Main-Class': 'fr.amynna.KeyUtil.KeyUtil' // Facultatif, si vous voulez une classe principale
    }
}

build.dependsOn shadowJar